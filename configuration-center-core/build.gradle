apply plugin: "jacoco"

dependencies {
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'com.google.guava:guava:23.0'
    compile 'io.reactivex.rxjava2:rxjava:2.1.1'
    compile 'org.apache.zookeeper:zookeeper:3.4.12'
    compile('org.apache.curator:curator-framework:4.0.1') {
        exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    }
    compile('org.apache.curator:curator-recipes:4.0.1') {
        exclude group: 'org.apache.zookeeper', module: 'zookeeper'
    }
    compile 'ch.qos.logback:logback-classic:1.2.3'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.21.0'
    testCompile('org.apache.curator:curator-test:4.0.1')
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

afterEvaluate {
    idea {
        module {
            testSourceDirs = testSourceDirs + sourceSets.integrationTest.allSource.srcDirs.flatten() as LinkedHashSet
            outputDir = new File("${buildDir}/classes/idea/main")
            testOutputDir = new File("${buildDir}/classes/idea/test")
        }
    }
}

sourceSets {
    test {
        java {
            srcDir 'src/test/unit/java'
        }
        resources {
            srcDir 'src/test/unit/resources'
        }
    }

    integrationTest {
        java {
            srcDir 'src/test/intg/java'
        }
        resources {
            srcDir 'src/test/intg/resources'
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

jacocoTestReport {
    sourceSets sourceSets.main
    executionData test, integrationTest

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    excludes: ['com/ndrlslz/configuration/center/core/model',
                               'com/ndrlslz/configuration/center/core/exception',
                               'spike']
            )
        })
    }
}

jacocoTestCoverageVerification {
    sourceSets sourceSets.main
    executionData test, integrationTest

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it,
                    excludes: ['com/ndrlslz/configuration/center/core/model',
                               'com/ndrlslz/configuration/center/core/exception',
                               'spike']
            )
        })
    }

    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 0.90
            }

            limit {
                counter = 'LINE'
                minimum = 0.90
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport

check.dependsOn integrationTest